
% Helper relations:

has_port(ProgramId, PortId) :- 
    has_out_port(ProgramId, PortId).
has_port(ProgramId, PortId) :-
    has_in_port(ProgramId, PortId). 

port_name(PortId, PortName) :-
    port(PortId, _, PortName, _).
port_name(PortId, PortName) :-
    port_alias(PortId, PortName). 

resource_metadata(ProgramName, PortName, ResourceId, VariableName, VariableValue) :-
    program(ProgramId, ProgramName, _, _), 
    has_port(ProgramId, PortId), 
    port_name(PortId, PortName), 
    channel(ChannelId, PortName), 
    resource_channel(ResourceId, ChannelId), 
    uri_variable(VariableId, VariableName, PortId), 
    uri_variable_value(ResourceId, VariableId, VariableValue). 
    

% Queries:

% Q1: What samples did the run of the script collect images from? 
q1(SampleId) :-
    resource_metadata("collect_data_set", "raw_image", _, "sample_id", SampleId). 

% Q2: What energies were used during collection of images from sample DRT322? 
q2(EnergyValue) :-
    resource_metadata("collect_data_set", "raw_image", ResourceId, "sample_id", "DRT322"),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "energy", EnergyValue).

% Q3: Where is the raw image corresponding to corrected image DRT322_11000ev_028.img? 
q3(File) :-
    resource_metadata("collect_data_set", "raw_image", ResourceId, "sample_id", "DRT322"),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "energy", "11000"),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "frame_number", "28"),
    resource(ResourceId, File).

% Q4: Are there any raw images for which no corrected image was written?
q4(File) :-
    resource(ResourceId, File),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "sample_id", SampleId),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "energy", Energy),
    resource_metadata("collect_data_set", "raw_image", ResourceId, "frame_number", FrameNumber),
    not corrected_image(SampleId, Energy, FrameNumber).

%corrected_image(SampleId, Energy, FrameNumber) :-
corrected_image(SampleId, Energy, FrameNumber) :-
    resource_metadata("transform_images", "corrected_image", ResourceId, "sample_id", SampleId),    
    resource_metadata("transform_images", "corrected_image", ResourceId, "energy", Energy),    
    resource_metadata("transform_images", "corrected_image", ResourceId, "frame_number", FrameNumber).

% Q5: Where is the spreadsheet that led to the corrected image DRT240_10000ev_010.img?
% q5(File) :-
q5(File) :-
    resource_metadata("collect_data_set", "raw_image", ResourceId1, "sample_id", "DRT240"),
    resource_metadata("collect_data_set", "raw_image", ResourceId1, "energy", "10000"),
    resource_metadata("collect_data_set", "raw_image", ResourceId1, "frame_number", "10"),
    resource_metadata("collect_data_set", "raw_image", ResourceId1, "cassette_id", CassetteId),
    resource_metadata("load_screening_results", "sample_spreadsheet", ResourceId2, "cassette_id", CassetteId), 
    resource(ResourceId2, File).
